CreateCardDeck()
ShuffleCardDeck()
'DisplayShuffledCards()
SetupButtons()

'
'---------------- subroutines -------------------
'
Sub DisplayShuffledCards
  'output the results
  For cardIndex = 1 to cardCount
    'note the accessing of one array with the value of another array
    'it's like one stack of boxes that has inside of each box the number of another box in a different stack
    'another way to do it would be to break it into more variables
    'randomCardIndex = shuffledCards[cardIndex]
    'oneCard = cardDeck[randomCardIndex]
    'instead of this
    oneCard = cardDeck[shuffledCards[cardIndex]]
    TextWindow.WriteLine(oneCard)
  EndFor
EndSub

'Modern Fisher-Yates shuffle
're-order the shuffled cards index array to a random sequence
Sub ShuffleCardDeck
  For oneCard = cardCount To 1 Step -1
    rand = Math.GetRandomNumber(oneCard)
    'TextWindow.WriteLine(shuffledCards[rand] + "(" + shuffledCards[oneCard] + ")")
    temp = shuffledCards[rand]
    shuffledCards[rand] = shuffledCards[oneCard]
    shuffledCards[oneCard] = temp
  EndFor
EndSub

Sub CreateCardDeck
  currentCardNumber = 1 ' init the card counter
  
  'put each card into the deck
  currentCardFace = "ace"
  AddSuitsForFace()
  currentCardFace = "2"
  AddSuitsForFace()
  currentCardFace = "3"
  AddSuitsForFace()
  currentCardFace = "4"
  AddSuitsForFace()
  currentCardFace = "5"
  AddSuitsForFace()
  currentCardFace = "6"
  AddSuitsForFace()
  currentCardFace = "7"
  AddSuitsForFace()
  currentCardFace = "8"
  AddSuitsForFace()
  currentCardFace = "9"
  AddSuitsForFace()
  currentCardFace = "10"
  AddSuitsForFace()
  currentCardFace = "jack"
  AddSuitsForFace()
  currentCardFace = "queen"
  AddSuitsForFace()
  currentCardFace = "king"
  AddSuitsForFace()
  
  cardCount = currentCardNumber-1 ' adjust count at the end for the 1 based array
EndSub

'subroutine to add each suit for a given card type
Sub AddSuitsForFace
  currentCardName = currentCardFace + "_of_spades"
  AddCardToDeck()
  currentCardName = currentCardFace + "_of_clubs"
  AddCardToDeck()
  currentCardName = currentCardFace + "_of_hearts"
  AddCardToDeck()
  currentCardName = currentCardFace + "_of_diamonds"
  AddCardToDeck()
EndSub

Sub AddCardToDeck
  'add a new card to the the array
  cardDeck[currentCardNumber] = currentCardName
  
  'initialize an array to an ordered sequence as we go along for when we want to shuffle
  shuffledCards[currentCardNumber] = currentCardNumber
  
  'increment the index number of the cards
  currentCardNumber = currentCardNumber + 1
EndSub

Sub SetupButtons
  GfxDisplay()
  cardToDeal = 1
  dealButton = Controls.AddButton("Deal Card ", 425, 1 )
  shuffleButton = Controls.AddButton("Re-Shuffle", 425, 28)
  Controls.ButtonClicked = OnButtonClicked
EndSub

Sub GfxDisplay
  GraphicsWindow.Width = "500"
  GraphicsWindow.Height = "726"
  GraphicsWindow.BackgroundColor = "Gray"
EndSub

Sub DisplayCard
  cardName = cardDeck[shuffledCards[cardToDeal]]
  ext = ".png"
  cardName =  "C:\SmallBasic\images\cards\" + cardName + ext"
  GraphicsWindow.DrawImage(cardName, 0, 0)
EndSub

Sub OnButtonClicked
  buttonClicked = Controls.LastClickedButton
  If buttonClicked = dealButton Then
    displayCard()
    If cardToDeal < cardCount Then
      cardToDeal = cardToDeal + 1
    EndIf
  ElseIf buttonClicked = shuffleButton then
    cardToDeal = 1
    ShuffleCardDeck()
    GraphicsWindow.Clear()
    SetupButtons()
  EndIf
EndSub
  
