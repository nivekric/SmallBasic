CreateCardDeck()
ShuffleCardDeck()
DisplayShuffledCards()

'
'---------------- subroutines -------------------
'
Sub DisplayShuffledCards
  'output the results
  For cardIndex = 1 to cardCount
    'note the accessing of one array with the value of another array
    'it's like one stack of boxes that has inside of each box the number of another box in a different stack
    'another way to do it would be to break it into more variables
    'randomCardIndex = shuffledCards[cardIndex]
    'oneCard = cardDeck[randomCardIndex]
    'instead of this
    oneCard = cardDeck[shuffledCards[cardIndex]]
    TextWindow.WriteLine(oneCard)
  EndFor
EndSub

'Modern Fisher-Yates shuffle
're-order the shuffled cards index array to a random sequence
Sub ShuffleCardDeck
  For oneCard = cardCount To 1 Step -1
    rand = Math.GetRandomNumber(oneCard)
    'TextWindow.WriteLine(shuffledCards[rand] + "(" + shuffledCards[oneCard] + ")")
    temp = shuffledCards[rand]
    shuffledCards[rand] = shuffledCards[oneCard]
    shuffledCards[oneCard] = temp
  EndFor
EndSub

Sub CreateCardDeck
  currentCardNumber = 1 ' init the card counter
  
  'put each card into the deck
  currentCardFace = "Ace"
  AddSuitsForFace()
  currentCardFace = "Two"
  AddSuitsForFace()
  currentCardFace = "Three"
  AddSuitsForFace()
  currentCardFace = "Four"
  AddSuitsForFace()
  currentCardFace = "Five"
  AddSuitsForFace()
  currentCardFace = "Six"
  AddSuitsForFace()
  currentCardFace = "Seven"
  AddSuitsForFace()
  currentCardFace = "Eight"
  AddSuitsForFace()
  currentCardFace = "Nine"
  AddSuitsForFace()
  currentCardFace = "Ten"
  AddSuitsForFace()
  currentCardFace = "Jack"
  AddSuitsForFace()
  currentCardFace = "Queen"
  AddSuitsForFace()
  currentCardFace = "King"
  AddSuitsForFace()
  
  cardCount = currentCardNumber-1 ' adjust count at the end for the 1 based array
EndSub

'subroutine to add each suit for a given card type
Sub AddSuitsForFace
  currentCardName = currentCardFace + " of Spades"
  AddCardToDeck()
  currentCardName = currentCardFace + " of Clubs"
  AddCardToDeck()
  currentCardName = currentCardFace + " of Hearts"
  AddCardToDeck()
  currentCardName = currentCardFace + " of Diamonds"
  AddCardToDeck()
EndSub

Sub AddCardToDeck
  'add a new card to the the array
  cardDeck[currentCardNumber] = currentCardName
  
  'initialize an array to an ordered sequence as we go along for when we want to shuffle
  shuffledCards[currentCardNumber] = currentCardNumber
  
  'increment the index number of the cards
  currentCardNumber = currentCardNumber + 1
EndSub
